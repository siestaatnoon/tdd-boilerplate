//
// Commented to make sample module the application module;
// to revert to application, uncomment and remove the
// apply plugin: 'com.android.library' line underneath AND
// uncomment the applicationId in defaultConfig { ... }
//
// apply plugin: 'com.android.application'

apply plugin: 'com.android.library'

android {
    defaultPublishConfig "debug"

    def globalConfiguration = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")

    defaultConfig {

        // Uncomment below to revert to application module
        // applicationId globalConfiguration.getAt("androidApplicationId")

        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        testApplicationId globalConfiguration.getAt("androidTestApplicationId")
        testInstrumentationRunner globalConfiguration.getAt("androidTestInstrumentationRunner")
        vectorDrawables.useSupportLibrary = true

        // Uncomment this to clear app data before each test when running with orchestrator
        // testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
        }

        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
    }

    testOptions {
        // uncomment this to run tests with orchestrator
        // execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    def dependencies = rootProject.ext.appDependencies
    def unitTestDependencies = rootProject.ext.appUnitTestDependencies
    def androidTestDependencies = rootProject.ext.appAndroidTestDependencies
    def devDependencies = rootProject.ext.appDevDependencies

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation dependencies.androidxLegacy
    implementation dependencies.androidxMaterial
    implementation dependencies.androidxAppCompat
    implementation dependencies.androidxPreference
    implementation dependencies.androidxCardView
    implementation dependencies.androidxRecyclerView
    implementation dependencies.androidxConstraintLayout
    implementation dependencies.dagger
    implementation dependencies.daggerAndroid
    implementation dependencies.daggerAndroidSupport
    annotationProcessor dependencies.daggerCompiler
    annotationProcessor dependencies.daggerAnnotation
    implementation dependencies.rxAndroid
    implementation dependencies.rxJava
    implementation dependencies.retrofit
    implementation dependencies.retrofitScalars
    implementation dependencies.retrofitRxJavaAdapter
    implementation dependencies.okhttp3Interceptor
    implementation dependencies.gson
    implementation dependencies.room
    annotationProcessor dependencies.roomCompiler

    // Unit Test dependencies
    testImplementation unitTestDependencies.junit
    testImplementation unitTestDependencies.androidxTestCore
    testImplementation unitTestDependencies.androidxTestRunner
    testImplementation unitTestDependencies.androidxTestJunit
    testImplementation unitTestDependencies.mockito
    testImplementation unitTestDependencies.robolectric
    // testImplementation unitTestDependencies.robolectricShadows // CURRENTLY NOT SUPPORTED WITH AndroidX
    testImplementation unitTestDependencies.roomTesting
    testImplementation unitTestDependencies.okhttp3MockWebServer
    testAnnotationProcessor unitTestDependencies.daggerCompiler

    // Android Instrumented Test dependencies
    androidTestImplementation androidTestDependencies.androidxAnnotation
    androidTestImplementation androidTestDependencies.androidxTestRules
    androidTestImplementation androidTestDependencies.androidxTestRunner
    androidTestImplementation androidTestDependencies.espressoCore
    androidTestImplementation androidTestDependencies.espressoContrib
    androidTestImplementation androidTestDependencies.espressoIntents
    androidTestImplementation androidTestDependencies.cucumber
    androidTestImplementation androidTestDependencies.cucumberPicoContainer
    androidTestImplementation androidTestDependencies.rxJavaIdler

    // Uncomment this to run instrumentation tests with orchestrator
    // androidTestUtil androidTestDependencies.androidxOrchestrator

    // Uncomment this to run instrumentation tests with Mockito
    // androidTestImplementation androidTestDependencies.mockito

    // Uncomment this to run instrumentation tests with Dagger 2
    // androidTestAnnotationProcessor androidTestDependencies.daggerCompiler

    // Development dependencies
    implementation devDependencies.timber
    debugImplementation devDependencies.leakCanary
    debugImplementation devDependencies.leakCanaryFragment
    releaseImplementation devDependencies.leakCanaryNoop
}
